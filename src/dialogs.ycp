/**
 * File:	include/tftp-server/dialogs.ycp
 * Package:	Configuration of tftp-server
 * Summary:	Dialogs definitions
 * Authors:	Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 */

{

textdomain "tftp-server";

import "Label";
import "Mode";
import "Popup";
import "Require";
import "TftpServer";
import "Wizard";

define term TextAndButton (term text, term button);
define void ViewLogPopup ();
define boolean CheckDirectorySyntax (string dir);


/**
 * Check for required packaged to be installed
 * @return `abort if aborted and `next otherwise
 */

define symbol Packages() ``{

   if (!Require::RequireAndConflictTarget (["xinetd", "tftp"], [],
	    // notification 1/2
	    _("<p>To configure the TFTP server, the packages <b>xinetd</b> and <b>tftp</b> must be installed.</p>") +
	    // notification 2/2
	    _("<p>Install them now?</p>")))
    {
        return `abort;
    }

	return `next;
}

/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
define symbol ReadDialog() ``{
    boolean ret = true;

    ret = ret && TftpServer::Read();

    if (TftpServer::foreign_servers != "")
    {
	Popup::Error (TftpServer::ForeignServersError ());
	ret = false;
    }
    return ret ? `next : `abort;
}

/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
define symbol WriteDialog() ``{
    boolean ret = TftpServer::Write();
    return ret ? `next : `abort;
}

/**
 * Main dialog
 * @return dialog result
 */
define symbol MainDialog () ``{
    Wizard::SetScreenShotName ("tftp-server-1-main");

    boolean start = TftpServer::start;
    string directory = TftpServer::directory;
    boolean changed = false;

    /* Tftp-server dialog caption */
    string caption = _("TFTP Server Configuration");

    // dialog help text
    string help_text = _("<p>Here, a server for TFTP (Trivial File Transfer Protocol) can be enabled. It will be started using xinetd.</p>");
    // dialog help text
    help_text = help_text + _("<p>Specify the directory where served files are located. The usual value is <tt>/tftpboot</tt>. It will be created if it does not exist. The server will use it as its root directory (using the <tt>-s</tt> option).</p>");

    term contents =
	`HVSquash (
	`VBox (
	    `RadioButtonGroup (
		`id (`rbg),
		`VBox (
		    `Left (`RadioButton (
			       `id (`tftpno), `opt (`notify),
			       // Radio button label, disable TFTP server
			       _("&Disable"), !start)),
		    `Left (`RadioButton (
			       `id (`tftpyes), `opt (`notify),
			       // Radio button label, disable TFTP server
			       _("&Enable"), start))
		    )
		),
	    `VSpacing (1),
	    TextAndButton (
		// Text entry label
		// Directory where served files (usually boot images) reside
		`TextEntry (`id (`directory), _("&Boot Image Directory"), directory),
		// push button label
		// select a directory from the filesystem
		`PushButton (`id (`browse), _("Bro&wse"))
		),
	    `VSpacing (2),
	    // push button label
	    // display a log file
	    `PushButton (`id (`viewlog), _("&View Log"))
	    )
	    );

    Wizard::SetContentsButtons(caption, contents, help_text,
	    Label::BackButton(), Label::FinishButton());

    any ret = nil;
	UI::ChangeWidget (`id (`viewlog), `Enabled, !Mode::config);
    repeat
    {
	UI::ChangeWidget (`id (`directory), `Enabled, start);
	UI::ChangeWidget (`id (`browse), `Enabled, start);

	ret = UI::UserInput();
	if (ret == `cancel)
	{
	    ret = `abort;
	}

	start = (UI::QueryWidget (`id (`rbg), `CurrentButton) == `tftpyes);
	directory = (string) UI::QueryWidget (`id (`directory), `Value);

	// discard the difference in disabled fields:
	// directory is only considered if start is on
	changed =
	    start != TftpServer::start ||
	    (start && directory != TftpServer::directory);

	if (ret == `browse)
	{
	    directory = UI::AskForExistingDirectory(directory != ""? directory: "/", "");
	    if (directory != nil)
	    {
		UI::ChangeWidget (`id (`directory), `Value, directory);
	    }
	}
	else if (ret == `viewlog)
	{
	    ViewLogPopup ();
	}

	// validity checks
	if (ret == `next && start)
	{
	    if (CheckDirectorySyntax (directory))
	    {
		//ok, say that it will be created
		import "Mode";
		if (!Mode::config && SCR::Read (.target.size, directory) < 0)
		{
		    // the dir does not exist
		    // yes-no popup
		    ret = Popup::YesNo(_("The directory does not exist.
Create it?")) ? ret : nil;
		}
	    }
	    else
	    {
		UI::SetFocus (`id (`directory));
		// error popup
		Popup::Error (_("The directory must start with a slash (/)
and must not contain spaces."));
		ret = nil;
	    }
	}
    }
    until (ret == `next ||
	   ((ret == `back || ret == `abort) &&
	    (!changed || Popup::ReallyAbort (true))));

    if (ret == `next)
    {
	TftpServer::start = start;
	if (start)
	{
	    TftpServer::directory = directory;
	}
    }

    Wizard::RestoreScreenShotName ();
    return (symbol) ret;
}

/**
 * Display a log in a popup
 * greps tftp from /var/log/messages to a temporary file
 * and uses Popup::ShowFile on it
 */
define void ViewLogPopup () ``{
    string tmpdir = (string) SCR::Read (.target.tmpdir);
    string tmpfile = tmpdir + "/tftp-log";
    string command = "/usr/bin/grep -i tftp /var/log/messages";
    SCR::Execute (.target.bash, command + " > " + tmpfile);
    // Log View widget caption
    // %1 is a command like "grep foo file"
    // snapshot means that the dialog will not display log items
    // added since the dialog was displayed
    Popup::ShowFile (sformat (_("Output of '%1' (snapshot)"), command), tmpfile);
    SCR::Execute (.target.remove, tmpfile);
}

// copied from nfs-client
/**
 * Nicely put a `TextEntry and its helper `PushButton together
 * @param text   textentry widget
 * @param button pushbutton widget
 * @return a HBox
 */
define term TextAndButton (term text, term button) ``{
    return `HBox (
	`Bottom (text),
	`HSpacing(0.5),
	`Bottom (button)
	);
}

/**
 * Must be an absolute path without whitespace.
 * @param dir directory path
 * @return valid?
 */
define boolean CheckDirectorySyntax (string dir) ``{
    return (substring (dir, 0, 1) == "/" && filterchars (" \t", dir) == "");
}

/* EOF */
}
